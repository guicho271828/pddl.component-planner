(DEFINE (DOMAIN DRIVERLOG)
 (:REQUIREMENTS :STRIPS)
 (:TYPES OBJ - OBJECT TRUCK - OBJECT LOCATION - OBJECT DRIVER - OBJECT)
 (:PREDICATES
  (EMPTY ?V - OBJECT)
  (PATH ?X - OBJECT ?Y - OBJECT)
  (LINK ?X - OBJECT ?Y - OBJECT)
  (DRIVING ?D - OBJECT ?V - OBJECT)
  (IN ?OBJ1 - OBJECT ?OBJ - OBJECT)
  (AT ?OBJ - OBJECT ?LOC - OBJECT))

 (:ACTION LOAD-TRUCK
  :PARAMETERS (?OBJ - OBJ ?TRUCK - TRUCK ?LOC - LOCATION)
  :PRECONDITION (AND (AT ?TRUCK ?LOC)
                     (AT ?OBJ ?LOC))
  :EFFECT (AND (NOT (AT ?OBJ ?LOC))
               (IN ?OBJ ?TRUCK)))

 (:ACTION UNLOAD-TRUCK
  :PARAMETERS (?OBJ - OBJ ?TRUCK - TRUCK ?LOC - LOCATION)
  :PRECONDITION (AND (AT ?TRUCK ?LOC)
                     (IN ?OBJ ?TRUCK))
  :EFFECT (AND (NOT (IN ?OBJ ?TRUCK))
               (AT ?OBJ ?LOC)))

 (:ACTION BOARD-TRUCK
  :PARAMETERS (?DRIVER - DRIVER ?TRUCK - TRUCK ?LOC - LOCATION)
  :PRECONDITION (AND (AT ?TRUCK ?LOC)
                     (AT ?DRIVER ?LOC)
                     (EMPTY ?TRUCK))
  :EFFECT (AND (NOT (AT ?DRIVER ?LOC))
               (DRIVING ?DRIVER ?TRUCK)
               (NOT (EMPTY ?TRUCK))))

 (:ACTION DISEMBARK-TRUCK
  :PARAMETERS (?DRIVER - DRIVER ?TRUCK - TRUCK ?LOC - LOCATION)
  :PRECONDITION (AND (AT ?TRUCK ?LOC)
                     (DRIVING ?DRIVER ?TRUCK))
  :EFFECT (AND (NOT (DRIVING ?DRIVER ?TRUCK))
               (AT ?DRIVER ?LOC)
               (EMPTY ?TRUCK)))

 (:ACTION DRIVE-TRUCK
  :PARAMETERS (?TRUCK - TRUCK ?LOC-FROM - LOCATION ?LOC-TO - LOCATION
               ?DRIVER - DRIVER)
  :PRECONDITION (AND (AT ?TRUCK ?LOC-FROM)
                     (DRIVING ?DRIVER ?TRUCK)
                     (LINK ?LOC-FROM ?LOC-TO))
  :EFFECT (AND (NOT (AT ?TRUCK ?LOC-FROM))
               (AT ?TRUCK ?LOC-TO)))

 (:ACTION WALK
  :PARAMETERS (?DRIVER - DRIVER ?LOC-FROM - LOCATION ?LOC-TO - LOCATION)
  :PRECONDITION (AND (AT ?DRIVER ?LOC-FROM)
                     (PATH ?LOC-FROM ?LOC-TO))
  :EFFECT (AND (NOT (AT ?DRIVER ?LOC-FROM))
               (AT ?DRIVER ?LOC-TO))))
