(DEFINE (DOMAIN MICONIC)
 (:REQUIREMENTS :STRIPS)
 (:TYPES FLOOR - OBJECT PASSENGER - OBJECT)
 (:PREDICATES
  (LIFT-AT ?FLOOR - OBJECT)
  (SERVED ?PERSON - OBJECT)
  (BOARDED ?PERSON - OBJECT)
  (ABOVE ?FLOOR1 - OBJECT ?FLOOR2 - OBJECT)
  (DESTIN ?PERSON - OBJECT ?FLOOR - OBJECT)
  (ORIGIN ?PERSON - OBJECT ?FLOOR - OBJECT))

 (:ACTION BOARD
  :PARAMETERS (?F - FLOOR ?P - PASSENGER)
  :PRECONDITION (AND (LIFT-AT ?F)
                     (ORIGIN ?P ?F))
  :EFFECT (BOARDED ?P))

 (:ACTION DEPART
  :PARAMETERS (?F - FLOOR ?P - PASSENGER)
  :PRECONDITION (AND (LIFT-AT ?F)
                     (DESTIN ?P ?F)
                     (BOARDED ?P))
  :EFFECT (AND (NOT (BOARDED ?P))
               (SERVED ?P)))

 (:ACTION UP
  :PARAMETERS (?F1 - FLOOR ?F2 - FLOOR)
  :PRECONDITION (AND (LIFT-AT ?F1)
                     (ABOVE ?F1 ?F2))
  :EFFECT (AND (LIFT-AT ?F2)
               (NOT (LIFT-AT ?F1))))

 (:ACTION DOWN
  :PARAMETERS (?F1 - FLOOR ?F2 - FLOOR)
  :PRECONDITION (AND (LIFT-AT ?F1)
                     (ABOVE ?F2 ?F1))
  :EFFECT (AND (LIFT-AT ?F2)
               (NOT (LIFT-AT ?F1)))))
